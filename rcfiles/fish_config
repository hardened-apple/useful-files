set fish_greeting

# Prompt set-up {{{
function fish_prompt

    # Only set up once (though it doesn't make much difference)
    if not set -q __user_color
        switch $USER
            case root
                set -g __user_color red
            case '*'
                set -g __user_color cyan
        end
    end

    if test -n "$SSH_CLIENT" -o -n "$SSH2_CLIENT" 
        set_color yellow; echo -n (hostname)
        set_color blue;echo -n :
    end

    set_color purple
    echo -n (basename (prompt_pwd))" "
    set_color blue; echo -n [
    set_color $__user_color; echo -n (date "+%H:%M:%S")
    set_color blue; echo -n ]
    set_color $__user_color; echo -n " > "
    set_color normal
end

function fish_right_prompt
    set -l git_branch (git branch 2> /dev/null | grep -e '\* ' | sed 's/^..\(.*\)/\1/')
    if [ -n "$git_branch" ]
        set_color blue; echo -n [
        set_color red; echo -n "git"
        set_color blue; echo -n ']['
        set_color green; echo -n $git_branch
        set_color blue; echo -n "] "
        set -l git_dirty (git diff)
        if [ -n "$git_dirty" ]
            set_color cyan; echo -n "U "
        end
    end
    # Fish doesn't do history like bash and zsh - so don't bother showing
    # history number
    set_color yellow; echo -n "[$status]"
    set_color normal
end
# }}}

# Bindings {{{
# Don't seem to have all the same functionality as in the other shells.
# e.g. incremental search, jump to character etc.
#
# Might be able to manually set up all these functions, but at the moment just
# have to deal with the smaller functionality.
function fish_user_key_bindings
   bind \e. 'history-token-search-backward'
end
# }}}

# Syntax highlighting for cat, head, and tail {{{
type highlight > /dev/null 2>&1
if [ $status -eq 0 ]
    function colored_command -d "Pipe given command through highlight with syntax from file extension"
        set -l syntax ""
        if [ -f $argv[-1] ]
            switch $argv[-1]
                case "*.lisp"    
                    set syntax "lisp"
                case "*.el"      
                    set syntax "lisp"
                case "*.asd"     
                    set syntax "lisp"
                case "*.java"    
                    set syntax "java"
                case "*.php"     
                    set syntax "php"
                case "*.py"      
                    set syntax "python"
                case "*.diff"    
                    set syntax "diff"
                case "*.rej"     
                    set syntax "diff"
                case "*.awk"     
                    set syntax "awk"
                case "*.c"       
                    set syntax "c"
                case "*.cpp"     
                    set syntax "cpp"
                case "*.ino"     
                    set syntax "cpp"
                case "*.pde"     
                    set syntax "cpp"
                case "*.css"     
                    set syntax "css"
                case "*.js"      
                    set syntax "js"
                case "*.jsp"     
                    set syntax "jsp"
                case "*.xml"     
                    set syntax "xml"
                case "*.sql"     
                    set syntax "sql"
                case "*.pl"      
                    set syntax "pl"
                case "*.rb"      
                    set syntax "rb"
                case "*.html"    
                    set syntax "html"
                case "*.sh"      
                    set syntax "sh"
                case "/etc/apache*/*"
                    set syntax "httpd"
            end
        end

        if [ $syntax != "" ]
            eval "command $argv | highlight -O ansi --syntax=$syntax"
        else
            eval "command $argv"
        end
    end

  function cat 
      eval "colored_command cat $argv"
  end

  function head
      eval "colored_command head $argv"
  end

  function tail
      eval "colored_command tail $argv"
  end


  # diff (always want the same highlighting)
  function colordiff
    eval "command diff $argv | highlight -O ansi --syntax=diff"
  end

end

# }}}

# Syntax highlighting for the command line {{{
# Fish has colors on the command line, can set everything up with these
# variables ...
#
# fish_color_autosuggestion
# fish_color_command
# fish_color_comment
# fish_color_cwd
# fish_color_cwd_root
# fish_color_error
# fish_color_escape
# fish_color_history_current
# fish_color_match
# fish_color_normal
# fish_color_operator
# fish_color_param
# fish_color_quote
# fish_color_redirection
# fish_color_search_match
# fish_color_valid_path
# fish_greeting
# fish_key_bindings
# fish_pager_color_completion
# fish_pager_color_description
# fish_pager_color_prefix
# fish_pager_color_progress
#
# }}}

# Alias's {{{
alias gr 'cd (git root)'
alias hr 'cd (hg root)'
# }}}

# Passwords functions {{{
set -U PASSDIR ~/.config/passwords

function pw
    pushd $PASSDIR
    if [ (count $argv) -eq 1 ]
        vim "$argv[1]"*
    end
    popd
end
# }}}

# vim: set foldmethod=marker:
